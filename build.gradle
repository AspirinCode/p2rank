apply plugin: 'groovy'
apply plugin: 'java'
apply plugin: 'idea'


group = 'cz.siret'
version = '2.0-rc.3_JAVAAPI'


description = 'Ligand binding site prediction based on machine learning.'

allprojects {
    apply plugin: 'java'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

repositories {

    jcenter()
    mavenCentral()
    mavenLocal()

	maven {
		url = "http://www.biojava.org/download/maven/"
	}
    maven {
		url = "https://maven.ch.cam.ac.uk/"
	}

    flatDir(dirs: 'lib')
}

sourceSets {
    main {
        resources {
            exclude 'scripts/out/*'
            exclude 'models/*'
        }
    }
}

processResources {
    filter org.apache.tools.ant.filters.ReplaceTokens, tokens: [
            "project_version": project.version
    ]
}

task(console, dependsOn: 'classes', type: JavaExec) {
    main = 'groovy.ui.Console'
    classpath = sourceSets.test.runtimeClasspath
}

task copyDependenciesToDist( type: Copy ) {
    into "$buildDir/../distro/bin/lib"
    from configurations.runtime
}

task copyBinaryToDist( type: Copy ) {
    dependsOn jar

    into "$buildDir/../distro/bin"
    from "$buildDir/bin"
    include "*.jar"
}

// task fatJar(type: Jar) {
//     baseName = project.name + '-all'
//     from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
//     with jar
// }

assemble {
    dependsOn processResources
    dependsOn copyBinaryToDist
    dependsOn copyDependenciesToDist
}

jar {
    archiveName = "p2rank.jar"
    destinationDir = file("build/bin")

    manifest {
        attributes 'Main-Class': 'cz.siret.p2rank.program.Main'
    }

    //fat jar: include all dependencies
    from {
        configurations.compile.filter( {! (it.name =~ /.*\.pom/ )}).collect { it.isDirectory() ? it : zipTree(it) }
    }
}

clean {
    delete "$buildDir/../distro/bin/lib"
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.7'
    compile 'org.codehaus.gpars:gpars:1.2.1'
    compile "org.apache.commons:commons-lang3:3.4"
    compile "com.google.guava:guava:19.0"

    compile 'org.slf4j:slf4j-api:1.7.21'
    compile 'org.apache.logging.log4j:log4j-slf4j-impl:2.5'

	compile 'org.biojava:biojava-core:4.2.4'
	compile 'org.biojava:biojava-alignment:4.2.4'
	compile 'org.biojava:biojava-structure:4.2.4-rdk1'

    compile 'org.openscience.cdk:cdk-qsarmolecular:1.5.13'    // for NumericalSurface class
    compile 'org.openscience.cdk:cdk-data:1.5.13'             // for Atom class
    // compile 'com.github.fommil.netlib:all:1.1.2'              // for weka
    compile 'nz.ac.waikato.cms.weka:weka-dev:3.9.0'
    compile files('lib/biojava-structure-4.2.4-rdk1.jar')     // customized fork for pocket-rank
    compile fileTree(dir: 'lib', include: '*.jar')

    testCompile 'junit:junit:4.12'
}

